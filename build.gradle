buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.jetbrains.dokka:dokka-gradle-plugin:0.9.17'
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.2.60'
    id 'maven'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.0'
}

apply plugin: 'org.jetbrains.dokka'

group = 'pw.aru.kt'
version = '1.2'

repositories {
    jcenter()
}

dependencies {
    compile 'org.jetbrains.kotlin:kotlin-stdlib:1.2.60'
    compile 'org.kodein.di:kodein-di-core-jvm:5.2.0'
    testCompile 'io.kotlintest:kotlintest-runner-junit5:3.1.7'
    testCompile 'org.kodein.di:kodein-di-generic-jvm:5.2.0'
}

test {
    useJUnitPlatform()
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from "$buildDir/javadoc"
}

task docsZip(type: Zip, dependsOn: dokka) {
    classifier = 'docs'
    from "$buildDir/dokka"
}

javadoc {
    failOnError = false
    options.memberLevel = JavadocMemberLevel.PUBLIC
    options.author()
    options.encoding = 'UTF-8'
    options.addStringOption('-html5')
}
dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
}

artifacts {
    archives javadocJar, sourcesJar, docsZip
}

publishing {
    publications {
        BintrayRelease(MavenPublication) {
            from components.java
            groupId group
            artifactId project.name
            version project.version
            artifact javadocJar
            artifact sourcesJar
        }
    }
}

bintray {
    user = bintrayUsername
    key = bintrayApiKey
    publications = ['BintrayRelease']
    pkg {
        repo = 'maven'
        name = project.name
        licenses = ['MIT']
        vcsUrl = 'https://github.com/arubot/kodein-jit-bindings.git'
        publish = true
        version {
            name = project.version
        }
    }
}

bintrayUpload {
    dependsOn build
    dependsOn 'publishBintrayReleasePublicationToMavenLocal'
}
